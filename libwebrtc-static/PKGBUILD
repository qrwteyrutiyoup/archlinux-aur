# Maintainer: Sergio Correia <sergio@correia.cc>

pkgname=libwebrtc-static
pkgver=4804
pkgrel=2
pkgdesc="A library that enables web browsers with Real-Time Communications (RTC) capabilities via simple Javascript APIs"
arch=('x86_64')
url="http://www.webrtc.org/"
license=('custom')
makedepends=('subversion' 'depot_tools-svn' 'python2' 'python2-colorama')
provides=('libwebrtc')
source=('01-fix-jni-peer-connection-include.patch'
        '02-dont-make-thin-archives.patch'
        'LICENSE'
        'PATENTS')
md5sums=('87731727eca34fbeedc9da10067b8155'
         '70532488ecbe385722d93d2c3ecb8e05'
         'ad296492125bc71530d06234d9bfebe0'
         '72e36e56fb537d57e92d8c8c76703b1c')

prepare() {
  mkdir -p "${srcdir}"/${pkgname}-${pkgver}
  cd "${srcdir}"/${pkgname}-${pkgver}

  # ugly workaround
  mkdir -p .bin
  ln -sf /usr/bin/python2 "$(pwd)"/.bin/python
  export PATH="$(pwd)"/.bin:"${PATH}"

  /opt/depot_tools-svn/gclient config http://webrtc.googlecode.com/svn/trunk
  /opt/depot_tools-svn/gclient sync --revision ${pkgver} --force

  cd trunk
  patch -uNp1 --reject-file=- -i ../../01-fix-jni-peer-connection-include.patch
  patch -uNp1 --reject-file=- -i ../../02-dont-make-thin-archives.patch
}

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}/trunk
  python2 build/gyp_chromium --depth=. -Dextra_gyp_flag=0 -Dinclude_tests=0 -Denable_google_now=0 -Dlogging=0 all.gyp
  /opt/depot_tools-svn/ninja -C out/Release
}

package() {
  cd "${srcdir}"
  for i in LICENSE PATENTS; do
    install -m644 -D ${i} "${pkgdir}"/usr/share/licenses/${pkgname}/${i}
  done

  cd "${srcdir}"/${pkgname}-${pkgver}/trunk

  # library
  mkdir -p .lib
  cd .lib
  rm -f *
  for l in $(find ../out/Release -name "*.a"); do
    ar x ${l}
  done

  ar rcs libWebRTC.a *.o
  install -m644 -D libWebRTC.a "${pkgdir}"/usr/lib/libWebRTC.a

  # includes
  cd "${srcdir}"/${pkgname}-${pkgver}/trunk
  for f in $(find . -name "*.h" | grep -E "^\.\/webrtc|^\.\/talk"); do
    install -m644 -D ${f} "${pkgdir}"/usr/include/libwebrtc/${f}
  done

}

# vim:set ts=2 sw=2 et:
