# Maintainer: Sergio Correia <sergio@correia.cc>
# Contributor: Isaac C. Aronson <isaac@pingas.org>

pkgname='icecream-git'
_gitname='icecream'
pkgver=0db4080
pkgrel=1
pkgdesc='Distributed compiler with a central scheduler to share build load.'
url='https://github.com/icecc/icecream'
license=('GPL')
depends=('gcc-libs' 'bash' 'libcap-ng')
provides=('icecream' 'icecream1')
optdepends=('iceberg-git: Fork of Icemon, an Icecc monitor.')
conflicts=('icecream1' 'icecream')
backup=('etc/icecream.conf')
arch=('i686' 'x86_64')
options=('!libtool')
install='icecream.install'

source=('git://github.com/icecc/icecream.git'
        'icecream.conf'
        'icecream-git.conf'
        'icecream.service'
        'icecream-scheduler.service'
        'icecreamd'
        'icecream-schedulerd'
        'remove-doc-subdir-from-makefile.patch')

md5sums=('SKIP'
         'b18cd84339230983b1e757e9f4fc03b1'
         'c613bbd36fd70be5dc937f1bf9694e70'
         '256dbfd08c2c7b51cbb62de3ae830afd'
         'b97228cf444282a995fa228b9969d7da'
         'fbfcedb664d8e98eb8fe00a3057b0ae5'
         '1e211761774a2a80e6a34ec0adefb70c'
         'c633806add24c9d55938202b209c54aa')

pkgver() {
    cd ${_gitname}

    # Use the tag of the last commit
    git describe --always | sed 's|-|.|g'
}

prepare() {
    cd ${_gitname}

    patch -uNp1 -i ../remove-doc-subdir-from-makefile.patch
}

build() {
    cd ${_gitname}

    ./autogen.sh
    ./configure --prefix=/usr/lib/icecream --enable-shared --disable-static --without-man
    make
}

package() {
    cd "${_gitname}"

    make DESTDIR="${pkgdir}" install
    install -D -m755 ../icecream.conf "${pkgdir}/etc/icecream.conf"
    install -D -m755 ../icecreamd "${pkgdir}/usr/lib/icecream/icecreamd"
    install -D -m755 ../icecream-schedulerd "${pkgdir}/usr/lib/icecream/icecream-schedulerd"
    install -D -m644 ../icecream-scheduler.service "${pkgdir}/usr/lib/systemd/system/icecream-scheduler.service"
    install -D -m644 ../icecream.service "${pkgdir}/usr/lib/systemd/system/icecream.service"
    install -D -m644 ../icecream-git.conf "${pkgdir}/etc/ld.so.conf.d/icecream-git.conf"

    # moving pkg-config file to its usual place
    install -D -m644 "${pkgdir}/usr/lib/icecream/lib/pkgconfig/icecc.pc" "${pkgdir}/usr/lib/pkgconfig/icecc.pc"
    rm -rf "${pkgdir}/usr/lib/icecream/lib/pkgconfig"
}
# vim:set ts=2 sw=2 et:
