# Maintainer: Sergio Correia <sergio.correia@uece.net>
# Contributor: Oleg Smirnov <oleg.smirnov@gmail.com>
# Contributor: Luke McCarthy <luke@iogopro.co.uk>

pkgname=ns
pkgver=2.35
pkgrel=3
pkgdesc="Discrete event simulator targeted at networking research"
url="http://www.isi.edu/nsnam/ns/"
license=('GPL')
depends=('gcc-libs' 'otcl')
makedepends=('tclcl')
arch=('i686' 'x86_64')
source=(http://downloads.sourceforge.net/sourceforge/nsnam/ns-2/$pkgver/ns-src-$pkgver.tar.gz
        ns-2.35-linkstate-erase.fix
        ns-2.35-tcl86.patch
        ns-2.35-getopts.patch)

md5sums=('0885b3fe2560bf63e75493481197fb6e'
         '8cefd7866550495099bf73605da6a57c'
         '3ca29e8fb1dad425a481e39a96a519b4'
         '850a87f5316b35342e7fe7a5056d6591')

optdepends=('nam: Tcl/TK based animation tool for viewing network simulation traces',
	    'xgraph: X-Windows application for interactive plotting and graphing')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    patch -Np1 -i ../ns-2.35-linkstate-erase.fix
    patch -Np1 -i ../ns-2.35-tcl86.patch
    patch -Np1 -i ../ns-2.35-getopts.patch
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    ./configure --prefix=/usr --with-tclcl=/usr/lib/tclcl
    make
}

#check() {
#    cd "${srcdir}"/${pkgname}-${pkgver}
#    make test
#}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    install -d "${pkgdir}"/usr/bin
    install -d "${pkgdir}"/usr/share
    install -d "${pkgdir}"/usr/man/man1

    make DESTDIR="${pkgdir}" install
    # fix man dir in the package
    mv ${pkgdir}/usr/man ${pkgdir}/usr/share
}
