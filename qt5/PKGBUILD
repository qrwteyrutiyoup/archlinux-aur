# Maintainer: Sergio Correia <sergio@correia.cc>

pkgname=qt5
pkgdesc='Qt is a cross-platform application and UI framework'
pkgver=5.0.0
pkgrel=3
arch=('i686' 'x86_64')
url='http://qt-project.org/'
license=('LGPL')
makedepends=('python2' 'ruby' 'gperf')
depends=('sqlite' 'libxrender' 'openal' 'systemd' 'libxslt' 'libtiff' 'libpng'
         'libmng' 'libgl' 'xcb-util-wm' 'xcb-util-renderutil' 'xcb-util-image'
         'xcb-util-keysyms' 'libxcomposite' 'postgresql-libs' 'libmysqlclient'
         'unixodbc' 'libfbclient' 'libpulse' 'gstreamer0.10-base' 'libegl'
    	 'libgles')
options=('!libtool')

_pkgfqn="qt-everywhere-opensource-src-${pkgver}"
source=("http://releases.qt-project.org/${pkgname}/${pkgver}/single/${_pkgfqn}.tar.gz"
        "qtwebkit-use-python2.patch")
md5sums=('1a1b73f913a965f6b0cf02bb820c8a03'
         '9f5fbd8497e379f60192a92d3ae0e280')

build() {
    cd ${_pkgfqn}

    # build with python2 (thanks to ascarpino for pointing out the patch at
    # https://projects.archlinux.org/svntogit/packages.git/plain/trunk/use-python2.patch?h=packages/qtwebkit
    patch -uNp1 -i ../qtwebkit-use-python2.patch

    ./configure --prefix=/usr \
        -release \
        -opensource \
        --confirm-license \
        -nomake demos \
        -nomake tests \
        -nomake examples \
        -nomake docs \
        -no-rpath \
        -bindir /usr/lib/qt5/bin \
        -headerdir /usr/include/qt5 \
        -archdatadir /usr/lib/qt5 \
        -plugindir /usr/lib/qt5/plugins \
        -importdir /usr/lib/qt5/imports \
        -qmldir /usr/lib/qt5/qml \
        -datadir /usr/share/qt5 \
        -sysconfdir /etc/xdg/qt5 \
        -docdir /usr/share/doc/qt5 \
        -examplesdir /usr/share/qt5/examples \
        -testsdir /usr/share/qt5/tests

    make
}

package() {
    cd ${_pkgfqn}

    make INSTALL_ROOT="${pkgdir}" install

    # removing compiled (binary) webkit examples; having the source
    # is good enough already
    for i in $(find ${pkgdir}/usr/share/qt5/examples -exec \
                file {} \; | grep -i elf | cut -d ':' -f 1); do
        rm -f ${i}
    done

    # creating -qt5 suffixed binaries in /usr/bin
    mkdir -p ${pkgdir}/usr/bin
    for i in $(ls ${pkgdir}/usr/lib/qt5/bin); do
        ln -s /usr/lib/qt5/bin/${i} ${pkgdir}/usr/bin/${i}-qt5
    done
}
