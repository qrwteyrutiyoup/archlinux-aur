# Maintainer: Sergio Correia <sergio@correia.cc>

pkgname='rubiksolver-git'
_pkgname='rubiksolver'
pkgver=5e356b7
pkgrel=2
pkgdesc="A Qt/QML application to assist with solving the popular Rubik's cube"
arch=('i686' 'x86_64')
url="https://github.com/qrwteyrutiyoup/rubiksolver"
license=('GPL')
depends=('qt5-quick1')
makedepends=('git')
source=('git://github.com/qrwteyrutiyoup/rubiksolver.git'
        '0001-porting-to-qt5.patch')
sha256sums=('SKIP'
            '4b81eeeb7e8054f6d8be9abd9690729efa61fcefb3d7daa24a3c352b8da0b74c')

pkgver() {
  cd "${_pkgname}"
  git describe --always | sed 's|-|.|g' # Use the tag of the last commit
}

prepare() {
  cd "${_pkgname}"
  patch -uNp1 -i ../0001-porting-to-qt5.patch
}

build() {
  cd "${_pkgname}"
  qmake-qt5 PREFIX=/usr
  make
}

package() {
  cd "${_pkgname}"
  make INSTALL_ROOT="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/share
}

# vim:set ts=2 sw=2 et:
