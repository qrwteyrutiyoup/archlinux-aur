# Maintainer: Sergio Correia <sergio.correia@uece.net>

pkgname='rubiksolver-git'
_gitname='rubiksolver'
pkgver=5e356b7
pkgrel=1
pkgdesc="A Qt/QML application to assist with solving the popular Rubik's cube"
arch=('i686' 'x86_64')
url="https://github.com/qrwteyrutiyoup/rubiksolver"
license=('GPL')
depends=('qt5-quick1')
makedepends=('git')
source=('git://github.com/qrwteyrutiyoup/rubiksolver.git'
        '0001-porting-to-qt5.patch')

md5sums=('SKIP'
         'f747a2e2c51086bc6920aa3f94d4adb7')

pkgver() {
    cd ${_gitname}

    # Use the tag of the last commit
    git describe --always | sed 's|-|.|g'
}

prepare() {
    cd ${_gitname}

    patch -uNp1 -i ../0001-porting-to-qt5.patch
}

build() {
    cd ${_gitname}

    qmake-qt5 PREFIX=/usr
    make
}

package() {
    cd "${_gitname}"

    make INSTALL_ROOT="${pkgdir}" install
    rm -rf ${pkgdir}/usr/share
}
