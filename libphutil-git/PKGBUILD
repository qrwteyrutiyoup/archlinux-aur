# Contributor: Nicolas Vivet <nizzox@gmail.com>
# Maintainer: Sergio Correia <sergio@correia.cc>

pkgname=libphutil-git
_gitname=libphutil
pkgver=conduit.5.298.gce82130
pkgrel=1
pkgdesc='Library system which organizes PHP classes and functions into modules'
arch=('any')
url="http://phabricator.com"
license=('Apache')
depends=('php')
makedepends=('git')
provides=('libphutil')
source=('git://github.com/facebook/libphutil.git')
md5sums=('SKIP')

pkgver() {
  cd $_gitname
  # Use the tag of the last commit
  git describe --always | sed 's|-|.|g'
}

package() {
  mkdir -p "$pkgdir/usr/share/php/$_gitname"
  # do not copy hidden directories
  cp -a $srcdir/$_gitname/* "$pkgdir/usr/share/php/$_gitname/"
}

# vim:set ts=2 sw=2 et:
