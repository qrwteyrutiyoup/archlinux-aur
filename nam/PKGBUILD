# Maintainer: Sergio Correia <sergio.correia@uece.net>
# Contributor: Oleg Smirnov <oleg.smirnov@gmail.com>
# Contributor: Luke McCarthy <luke@iogopro.co.uk>

pkgname=nam
pkgver=1.15
pkgrel=2
pkgdesc="Tcl/TK based animation tool for viewing network simulation traces and real world packet traces"
url="http://www.isi.edu/nsnam/nam/"
license=('GPL')
arch=('i686' 'x86_64')
depends=('gcc-libs' 'otcl' 'sh')
makedepends=('tclcl')
source=(http://downloads.sourceforge.net/sourceforge/nsnam/nam-1/$pkgver/nam-src-$pkgver.tar.gz
        nam-1.15-tcl86.patch)

md5sums=('1cf6646acc2f82e59965f5969f5a9239'
         '960e41cf84a168c7660aeb2195835b24')

prepare() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    patch -Np1 -i ../nam-1.15-tcl86.patch
}

build() {
    cd "${srcdir}"/${pkgname}-${pkgver}
    ./configure --prefix=/usr --with-tclcl=/usr/lib/tclcl
    make
}

package() {
    cd "${srcdir}"/${pkgname}-${pkgver}

    install -d "${pkgdir}"/usr/bin
    make DESTDIR="${pkgdir}" install

    for dir in edu ex iecdemos bin; do
        install -dm755 "${pkgdir}"/usr/share/nam/$dir
        install -m644 -t "${pkgdir}"/usr/share/nam/$dir $dir/*
    done
}
